grammar

var, x, y :: '' ::= {{ com variables }}

nat, i, j, n :: '' ::= {{ com natural numbers }}
  | 0 :: :: zero
  | S i :: :: succ {{ tex 1 + [[i]] }}
  | ( i ) :: S :: Paren
  | | G | :: S :: Length
  | (# i #) :: S :: SecretParen
    {{ tex [[i]] }}

set, S, F, I :: '' ::= {{ com sets }}
  | emptyset :: :: empty
    {{ tex \emptyset }}
  | { a | formula } :: :: comprehension
    {{ tex \{ [[a]] \mid [[formula]]  \} }}
  | dom S :: :: domain
    {{ tex [[dom]]([[S]]) }}
  | F a   :: :: App
    {{ tex [[F]]([[a]]) }}
  | STm   :: :: SetOfTerms
    {{ tex \textit{Term} }}
  | PowerSet S :: :: PowerSet
    {{ tex \mathcal{P}([[S]]) }}
  | S1 -> S2 :: :: FunSet
    {{ tex [[S1]] \rightarrow [[S2]] }}
  | (# S #) :: :: HParen
    {{ tex [[S]] }}
  | { i | formula  } :: :: comprehensionNat
    {{ tex \{ [[i]] \mid [[formula]]  \} }}

tm, a , b , c, t, p, A, B, C :: 'a_' ::= {{ com terms and types }}
  | arg :: :: Hole
    {{ tex - }}
  | i :: :: Var
    {{ com variable }}
  | Pi A B :: :: Pi
    {{ com dependent function type }}
  | \ A a :: :: Abs
    {{ tex \lambda [[A]] \mathpunct{.} [[a]] }}
    {{ com function }}
  | \\ a   :: :: UAbs
    {{ tex \lambda [[a]] }}
  | a b :: :: App
    {{ tex [[a]] \; [[b]] }}
    {{ com function application }}
  | Set i :: :: Set
    {{ com universe }}
  | a { b } :: S :: Subst {{ tex [[a]] \langle [[b]]  \rangle }}
  | a { b0 , b1 } :: S :: SubstTwo {{ tex [[a]] \langle [[b0]] , [[b1]]  \rangle }}
  | ( A )   :: M :: Paren {{ coq ([[A]]) }}
  | (# a #) :: S :: SecretParen
    {{ tex [[a]] }}
    {{ com parsing precedence is hard }}
  | (@ a @) :: M :: TexParen
    {{ tex ([[a]]) }}
  | a ~ b : A :: :: Eq
    {{ com identity type }}
    {{ tex [[a]] \; \sim \; [[b]] \in {[[A]]} }}
  | refl      :: :: Refl
    {{ com reflexivity proof }}
  | Void      :: :: Void
    {{ com empty type }}
  | J t a b p         :: :: Transp
    {{ com J eliminator }}
  | Bool           :: :: Bool
    {{ tex \mathbb{B} }}
    {{ com boolean type }}
  | true :: :: True
  | false :: :: False
  | if a b0 b1 :: :: If
    {{ tex \ottkw{if} \; [[a]]\; \ottkw{then}\; [[b0]]\; \ottkw{else}\; [[b1]] }}
  | up n A :: S :: Shift
    {{ tex \uparrow^{[[n]]} [[A]] }}
  | up1 A :: S :: ShiftOne
    {{ tex \uparrow[[A]] }}
  | G i :: S :: lookup
    {{ tex [[G]]_{[[i]]} }}
  | A -> B :: M :: Arr
    {{ tex [[A]] \rightarrow [[B]] }}

context, G {{ tex \Gamma }} :: 'ctx_' ::= {{ com contexts }}
					  {{ coq list ( atom * (grade * tm) ) }}
   | empty           ::    :: Empty       {{ coq nil }}
   | A               :: M   :: One
   | G ++ A          ::   :: Append      {{ tex [[G]], [[A]] }}
   | ( G )           :: M  :: Paren       {{ coq ([[G]]) }}
   | (# G #)           :: M  :: SecretParen {{ tex [[G]] }}

terminals :: 'terminals_' ::=
  | <->               ::   :: iso           {{ tex \leftrightarrow }}
  | <=>               ::   :: lrarrow       {{ tex \Leftrightarrow }}
  | -->               ::   :: step          {{ tex \longrightarrow }}
  | min               ::   :: min           {{ tex \mathsf{min} }}
  | ==                ::   :: definitional  {{ tex \equiv }}
  | all               ::   :: forall     {{ tex \forall }}
  | in                ::   :: in         {{ tex \in }}
  | let               ::   :: let        {{ tex \ottkw{let} }}
  | bind              ::   :: bind       {{ tex \ottkw{bind} }}
  | notin             ::   :: notin      {{ tex \not\in }}
  | <=                ::   :: check      {{ tex \leq }}
  | =>                ::   :: infer      {{ tex \Rightarrow }}
  | =>+               ::   :: minfer      {{ tex \Rightarrow^{\ast} }}
  | ->                ::   :: arrow      {{ tex \to }}
  | <-|               ::   :: mapsfrom   {{ tex \mapsfrom }}
  | []                ::   :: box        {{ tex \Box }}
  | |-                ::   :: entails    {{ tex \vdash }}
  | |-e               ::   :: entailsE   {{ tex \vdash }}
  | |-e               ::   :: entailsEE  {{ tex \vdash }}
  | |-$               ::   :: entailsC   {{ tex \vdash_{C} }}
  | -|                ::   :: produces   {{ tex \dashv }}
  | |=                ::   :: hs_entails {{ tex \vDash }}
  | |==               ::   :: hs_entaill {{ tex \vDash }}
  | /=                ::   :: neq        {{ tex \neq }}
  | |>                ::   :: cast       {{ tex \triangleright }}
  | ok                ::   :: ok         {{ tex \ \mathsf{ok} }}
  | _                 ::   :: blank      {{ tex \_ }}
  | ~>                ::   :: produce    {{ tex \leadsto }}
  | ~>*               ::   :: multistep  {{ tex \leadsto^{\ast} }}
  | ~>h               ::   :: steph      {{ tex \leadsto }}
  | empty             ::   :: empty      {{ tex \cdot }}
  | fv                ::   :: fv         {{ tex \mathsf{fv}\! }}
  | dom               ::   :: dom        {{ tex \mathsf{dom} }}
  | ~                 ::   :: twiddle    {{ tex \sim }}
  | ~~                ::   :: compatible {{ tex \asymp }}
  | '|'               ::   :: bar        {{ tex | }}
  | o                 ::   :: bullet     {{ tex \bullet }}
  | fst               ::   :: fst
  | snd               ::   :: snd
  | |=>|              ::   :: erasesto   {{ tex \mathrel{|\Rightarrow|} }}
  | |-=               ::   :: turneq     {{ tex \vdash_{=} }}
  | refl2             ::   :: refl2      {{ tex \mathbf{refl_2}  }}
  | ++                ::   :: concat     {{ tex \mathop{++} }}
  | *                 ::   :: times      {{ tex \cdot }}
  | Box               ::   :: Box        {{ tex \ottkw{Box} }}
  | =beta             ::   :: betaeq     {{ tex =_{\beta} }}
  | ;                 ::   :: semi       {{ tex \mathsf{;} }}
  | Pi                ::   :: Pi         {{ tex \Pi }}
  | inj               ::   :: inj        {{ tex \ottkw{inj} }}
  | case              ::   :: case       {{ tex \ottkw{case} }}
  | /\                ::   :: meet       {{ tex \wedge }}
  | \/                ::   :: join       {{ tex \vee }}
  | |->               ::   :: ctyp       {{ tex \Vdash }}
  | implies           ::   :: implies    {{ tex \ottkw{implies} }}
  | transp            ::   :: transp     {{ tex \ottkw{transp} }}

formula :: 'formula_' ::=
  | i < j                     ::   :: lt
  | judgement                 ::   :: judgement
  | a in S                    ::   :: tmembership
  | S1 in S2                    ::   :: fmembership
  | formula1 \/ formula2      ::   :: or
  | formula1 , formula2       ::   :: and
  | formula1 implies formula2 ::   :: implies
    {{ tex [[formula1]]\implies [[formula2]] }}
  | forall a , formula        ::   :: forallTm
    {{ tex \forall [[a]], [[formula]] }}
  | exists S , formula    ::   :: existsSet
    {{ tex \exists [[S]], [[formula]] }}
  | (# formula  #)            ::   :: HParen
    {{ tex [[formula]] }}
  | S1 = S2                   ::   :: SetEq


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%Dependent Types

defns
   JPar :: '' ::=

defn
 a => b :: :: Par :: 'P_' {{ com Parallel Reduction }}
 {{ tex [[ a ]] \Rightarrow [[ b ]]  }}
by

----------- :: Var
i => i

---------------- :: Set
Set i => Set i

-------------- :: Void
Void => Void

A0 => A1
B0 => B1
----------------------- :: Pi
Pi A0 B0 => Pi A1 B1

A0 => A1
a0 => a1
----------------------- :: Abs
\ A0 a0 => \ A1 a1

a0 => a1
b0 => b1
----------------------- :: App
a0 b0 => a1 b1

a => \ A a0
b0 => b1
----------------------- :: AppAbs
a b0 => a0 { b1 }

-------------- :: True
true => true

--------------- :: False
false => false

a0 => a1
b0 => b1
c0 => c1
--------------------------- :: If
if a0 b0 c0 => if a1 b1 c1

a0 => true
b0 => b1
c0 => c1
--------------------------- :: IfTrue
if a0 b0 c0 => b1

a0 => false
b0 => b1
c0 => c1
--------------------------- :: IfFalse
if a0 b0 c0 => c1

-------------- :: Bool
Bool => Bool

a0 => a1
b0 => b1
A0 => A1
----------------------------- :: Eq
a0 ~ b0 : A0 => a1 ~ b1 : A1

------------- :: Refl
refl => refl

t0 => t1
a0 => a1
b0 => b1
p0 => p1
------------------------------- :: J
J t0 a0 b0 p0 => J t1 a1 b1 p1

t0 => t1
a0 => a1
b0 => b1
p => refl
-------------------- :: JRefl
J t0 a0 b0 p => t1


defns
JPars :: '' ::=

defn
 a =>+ b :: :: Pars :: 'PS_' {{ com Parallel Reduction }}
 {{ tex [[ a ]] \Rightarrow^+ [[ b ]]  }}
by

a => b
---------- :: One
a =>+ b

a => b
b =>+ c
------------------ :: Step
a =>+ c


defns
JJoin :: '' ::=

defn
a <=> b :: :: Coherent :: 'C_' {{ com Coherence }}
{{ tex [[a]] \Leftrightarrow [[b]] }}
by

a =>+ c
b =>+ c
----------------------- :: Intro
a <=> b

defns
JTyping :: '' ::=

defn
|- G ::     :: Ctx :: 'Ctx_' {{ com Context Well-Formedness }}
{{ tex \vdash [[G]] }}
by

------------- :: Empty
|- empty


|- G
G |- A : Set i
------------------------- :: Cons
|- G ++ A

defn
  G |- a : A ::  :: Typing :: 'T_' {{ com Typing }}
{{ tex [[G]] \vdash [[a]]: [[A]] }}
 by

|- G
i < j
-------------------------- :: Set
G |-  Set i : Set j

|- G
i < | G |
--------------------------------------------- :: Var
G |- i : up S i G i

%% functions

G |- A : Set i
G ++ A |- B : Set i
--------------------------------------- :: Pi
G |- Pi A B : Set i

G ++ A |- b : B
G |- Pi A B : Set i
----------------------------------------------- :: Abs
G |- \ A b : Pi A B

G |- b : Pi A B
G |- a : A
------------------------------------------- :: App
G |- b a : B { a }

G |- a : A
G |- B : Set i
A <=> B
------------------------------- :: Conv
G |- a : B

|- G
G |- a : A
-------------------------------- :: Refl
G |- refl : a ~ a : A

G |- a : A
G |- b : A
G |- A : Set j
-------------------------------- :: Eq
G |- a ~ b : A : Set i

G |- a : A
G |- b : A
G |- A : Set j
G |- p : a ~ b : A
(# G ++ A #) ++ 0 ~ up1 a : up1 A |- C : Set i
G |- t : C { a ,  refl }
------------------------------------ :: J
G |- J t a b p : C { b , p }

|- G
-------------------------- :: Bool
G |- Bool : Set i

|- G
----------------------------- :: True
G |- true : Bool

|- G
----------------------------- :: False
G |- false : Bool

G |- a : Bool
G |- b0 : A
G |- b1 : A
----------------------------- :: If
G |- if a b0 b1 : A

|- G
----------------------------- :: Void
G |- Void : Set i


defns
JI :: '' ::=

defn
Interp I i A S ::  :: Interp :: 'I_' {{ com Interpretation relation }}
{{ tex [ [[A]] ]_[[i]]^[[I]] \searrow [[S]] }}
by

--------------------------- :: Void
Interp I i Void emptyset


--------------------------- :: Bool
Interp I i Bool { a | a =>+ true \/ a =>+ false   }


--------------------------------------- :: Eq
Interp I i a ~ b : A { p | p =>+ refl , a <=> b  }


Interp I i A S
F in S -> PowerSet STm
forall a, (# a in S implies Interp I i B { a } F a #)
-------------------------------- :: Pi
Interp I i Pi A B { b | forall a, (# a in S implies b a in F a #) }


j < i
--------------------------------- :: Set
Interp I i Set j I j


A => B
Interp I i B S
--------------------------------- :: Red
Interp I i A S

defns
JII :: '' ::=

defn
InterpR i A S ::  :: InterpR :: 'I_' {{ com Interpretation relation }}
{{ tex [ [[A]] ]_[[i]] \searrow [[S]] }}
by

% Interp ( exists S , InterpR i A S )
% ---------------------- :: Rec
% InterpR i A S